// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'constants.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:3.1.1'
        classpath 'com.jakewharton:butterknife-gradle-plugin:9.0.0-SNAPSHOT'
        classpath 'io.fabric.tools:gradle:1.24.4'
        classpath 'com.google.firebase:firebase-plugins:1.1.1'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

subprojects {
    afterEvaluate {
        def isAndroidProject = project.plugins.hasPlugin('android')
        android {
            dataBinding {
                enabled = true
            }
            dexOptions {
                maxProcessCount 2
                javaMaxHeapSize "2g"
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            if (isAndroidProject) {
                signingConfigs {
                    release {
                        storeFile file(rootProject.file("nas2skupa.jks"))
                        storePassword keystoreProperties['storePassword']
                        keyAlias keystoreProperties['keyAlias']
                        keyPassword keystoreProperties['keyPassword']
                    }
                }
                buildTypes {
                    release {
                        signingConfig signingConfigs.release
                        lintOptions {
                            disable 'MissingTranslation'
                        }
                    }
                }
            }
        }
        dependencies {
            annotationProcessor "org.androidannotations:androidannotations:$androidAnnotationsVersion"
            compile "org.androidannotations:androidannotations-api:$androidAnnotationsVersion"
        }
        configurations.all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                def requested = details.requested
                if (requested.group == 'com.android.support') {
                    if (!requested.name.startsWith("multidex")) {
                        details.useVersion "$supportLibraryVersion"
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
